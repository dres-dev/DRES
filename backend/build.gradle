import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-stdlib:$version_kotlin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$version_kotlin"
    }
}

apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'
apply plugin: 'idea'

mainClassName = 'dev.dres.DRES'
sourceCompatibility = 11

/* Configuration for frontend classpath files (see dependencies). */
configurations {
    frontendClasspath {
        canBeConsumed = false
        canBeResolved = true
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.reposilite.com/snapshots'
        allowInsecureProtocol = true
    } //javalin openapi snapshot
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
    kotlinOptions {
        freeCompilerArgs = ["-Xinline-classes"]
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

dependencies {
    def javalin = '5.3.2'
    def javalinOpenapi = '5.3.3-SNAPSHOT'
    def log4jVersion = '2.17.1'

    ///// Frontend files (produced by sub-project).
    implementation frontendClasspath(project(path: ":frontend", configuration: 'frontendFiles'))

    ///// FFMpeg dependency
    implementation files("$buildDir/ext") {builtBy 'setupFFMpeg'}

    ///// Xodus & Xodus DNQ
    implementation group: 'org.jetbrains.xodus', name: 'xodus-openAPI', version: version_xodus
    implementation group: 'org.jetbrains.xodus', name: 'xodus-environment', version: version_xodus
    implementation group: 'org.jetbrains.xodus', name: 'xodus-entity-store', version: version_xodus
    implementation group: 'org.jetbrains.xodus', name: 'xodus-vfs', version:  version_xodus
    implementation group: 'org.jetbrains.xodus', name: 'dnq', version: version_xodus_dnq

    ////// Javalin
    implementation group: 'io.javalin', name:  'javalin', version: javalin
    kapt("io.javalin.community.openapi:openapi-annotation-processor:$javalinOpenapi")

    implementation group: 'io.javalin.community.openapi', name: 'javalin-openapi-plugin', version: javalinOpenapi
    implementation group: 'io.javalin.community.openapi', name:'javalin-swagger-plugin', version: javalinOpenapi
    implementation group: 'io.javalin.community.ssl', name: 'ssl-plugin', version: javalin

    ////// Bcrypt
    implementation group: 'org.mindrot', name: 'jbcrypt', version: version_bcrypt

    ////// JLine 3, Clikt & Picnic for optimal terminal experience :-)
    implementation group: 'org.jline', name: 'jline-terminal', version: version_jline3
    implementation group: 'org.jline', name: 'jline-terminal-jna', version: version_jline3
    implementation group: 'org.jline', name: 'jline-reader', version: version_jline3
    implementation group: 'org.jline', name: 'jline-builtins', version: version_jline3
    implementation group: 'com.github.ajalt', name: 'clikt', version: version_clikt
    implementation group: 'com.jakewharton.picnic', name: 'picnic', version: version_picnic

    ///// Fuel
    implementation group: 'com.github.kittinunf.fuel', name: 'fuel', version: version_fuel

    ////// CSV
    implementation group: 'com.github.doyaaaaaken', name: 'kotlin-csv-jvm', version: version_kotlin_csv

    ////// Jaffree ffmpeg wrapper
    implementation group: 'com.github.kokorin.jaffree', name: 'jaffree', version: version_jaffree

    ////// Cache
    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.8.6'

    ////// Log4J
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: version_log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: version_log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: version_log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-jul', version: version_log4j

    ////// FastUtil
    implementation group: 'it.unimi.dsi', name: 'fastutil', version: version_fastutil

    ///// JUnit 5
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: version_junit
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: version_junit
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: version_junit

    ///// Jackson / Kotlin
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.13.0'

    ///// Kotlin
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: version_kotlin
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: version_kotlin
}

kapt {
    correctErrorTypes true
    useBuildCache true
}

test {
    useJUnitPlatform()
}

idea {
    module {
        downloadJavadoc = true
    }
}

sourceSets {
    main {
        resources {
            srcDir 'config'
        }
    }
}

distributions {
    main {
        distributionBaseName = 'dres-dist'
    }
}

/** Custom tasks: FFmpeg Download and deployment. */

task downloadFFmpeg(type: Download) {
    def f = new File("$buildDir/cache/ffmpeg.zip")

    def os = ""
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        os = "win"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        os = "osx"
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        os = "linux"
    }

    src "https://github.com/vot/ffbinaries-prebuilt/releases/download/v4.2.1/ffmpeg-4.2.1-$os-64.zip"
    dest f
    onlyIf {
        !f.exists()
    }
}

task downloadFFprobe(type: Download) {
    def f = new File("$buildDir/cache/ffprobe.zip")

    def os = ""
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        os = "win"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        os = "osx"
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        os = "linux"
    }

    src "https://github.com/vot/ffbinaries-prebuilt/releases/download/v4.2.1/ffprobe-4.2.1-$os-64.zip"
    dest f
    onlyIf {
        !f.exists()
    }

}

task copyFFmpeg(type: Copy) {
    dependsOn downloadFFmpeg
    outputs.upToDateWhen {
        return !fileTree("$buildDir/ext/ffmpeg").filter { it.isFile() && it.name.startsWith('ffmpeg')}.isEmpty()
    }
    from zipTree(downloadFFmpeg.dest)
    into "$buildDir/ext/ffmpeg"
    include '*ffmpeg*'
}

task copyFFprobe(type: Copy) {
    dependsOn downloadFFprobe
    outputs.upToDateWhen {
        return !fileTree("$buildDir/ext/ffmpeg").filter { it.isFile() && it.name.startsWith('ffprobe') }.isEmpty()
    }
    from zipTree(downloadFFprobe.dest)
    into "$buildDir/ext/ffmpeg"
    include '*ffprobe*'
}

task setupFFMpeg(type: Copy) {
    dependsOn downloadFFmpeg
    dependsOn downloadFFprobe
    dependsOn copyFFmpeg
    dependsOn copyFFprobe
}

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}
