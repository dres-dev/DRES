import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    ext.kotlinVersion = '1.4.31'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
    }
}

plugins {
    /// Download task for FFMPEG binaries download
    id 'de.undercouch.download' version '4.0.4'
    /// OpenAPI Generator for Frontend internal api generation
    id 'org.openapi.generator' version '5.0.0'
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'idea'

mainClassName = 'dev.dres.DRES'
sourceCompatibility = 1.8

/* Configuration for frontent classpath files (see dependencies). */
configurations {
    frontendClasspath {
        canBeConsumed = false
        canBeResolved = true
    }
}

/* Includes the frontendClasspath files into the JAR. */
jar {
    into('html') {
        from configurations.frontendClasspath
    }
}

/// Default OpenAPI Specifications of dres
def fullOAS = "http://localhost:8080/swagger-docs"
def clientOAS = "http://localhost:8080/client-oas"
def oasFile = "${project.projectDir}/../doc/oas.json"
def clientOasFile = "${project.projectDir}/../doc/oas-client.json"
if(project.hasProperty('oas')){
    fullOAS = oas
}

/// Generates the openapi frontend bindings
openApiGenerate {
    /// Source command:
    /// openapi-generator generate -g typescript-angular -i http://localhost:8080/swagger-docs -o openapi --skip-validate-spec --additional-properties npmName=@dres-openapi/api,snapshot=true,ngVersion=9.1.0
    generateApiTests = false // No tests please
    generateModelTests = false // No tests please
    validateSpec = false // No validation please (as in command above)

    generatorName = 'typescript-angular'
    inputSpec = oasFile
    outputDir = file("${project.projectDir}/../frontend/openapi").toString()
    configOptions = [
        npmName: '@dres-openapi/api',
        ngVersion: '9.1.0',
        snapshot: 'true', /// I suggest to remove this, as soon as we automate this,
        enumPropertyNaming: 'original'
    ]
}

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        freeCompilerArgs = ["-Xinline-classes"]
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    def jettyVersion = '9.4.25.v20191220'
    def alpnApiVersion = '1.1.3.v20160715'
    def alpnBootVersion = '8.1.12.v20180117'

    frontendClasspath(project(path: ":frontend", configuration: 'frontendFiles'))

    ///// MapDB
    compile group: 'org.mapdb', name: 'mapdb', version: '3.0.8'

    ////// JLine 3
    compile group: 'org.jline', name: 'jline', version: '3.15.0'

    ////// Javalin
    compile group: 'io.javalin', name: 'javalin-bundle', version: '3.13.0'
    compile group: 'io.swagger.core.v3', name: 'swagger-core', version: '2.1.5'
    compile group: 'org.webjars', name: 'swagger-ui', version: '3.24.3'
    compile group: 'org.webjars.npm', name: 'js-tokens', version: '3.0.2'
    compile group: 'org.webjars.npm', name: 'redoc', version: '2.0.0-rc.2'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.12.+'
    compile group: 'io.github.classgraph', name: 'classgraph', version: '4.8.34'
    compile group: 'org.eclipse.jetty.http2', name: 'http2-server', version: jettyVersion
    compile group: 'org.eclipse.jetty', name: 'jetty-alpn-conscrypt-server', version: jettyVersion
    compile group: 'org.eclipse.jetty.alpn', name: 'alpn-api', version: alpnApiVersion
    runtime group: 'org.mortbay.jetty.alpn', name: 'alpn-boot', version: alpnBootVersion

    ////// bcrypt
    compile group: 'org.mindrot', name: 'jbcrypt', version: '0.4'

    ////// CLIKT
    compile group: 'com.github.ajalt', name: 'clikt', version: '2.6.0'

    ///// Picnic
    compile group: 'com.jakewharton.picnic', name: 'picnic', version: '0.3.1'

    ///// Fuel
    compile group: 'com.github.kittinunf.fuel', name: 'fuel', version: '2.3.1'

    ////// CSV
    compile group: 'com.github.doyaaaaaken', name: 'kotlin-csv-jvm', version: '0.7.3'

    ////// Jaffree ffmpeg wrapper
    compile group: 'com.github.kokorin.jaffree', name: 'jaffree', version: '0.9.3'

    ////// Cache
    compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.8.6'

    ////// Log4J
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.13.1'

    ///// JUnit 5

    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit5"

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0'

    ///// Kotlin
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion
}

test {
    useJUnitPlatform()
}

idea {
    module {
        downloadJavadoc = true
    }
}

sourceSets {
    main {
        resources {
            srcDir 'config'
        }
    }
}

distributions {
    main {
        distributionBaseName = 'dres-dist'
        contents {
            from 'ext'
        }
    }
}

task generateOAS(type: Download) {
    /* Requires DRES running on default port */
    // TODO Direct write to file instead of download
    def f = new File(oasFile)
    src "$fullOAS"
    dest f
}

task generateClientOAS(type: Download){
    /* Requires DRES running on default port */
    def f = new File(clientOasFile)
    src "$clientOAS"
    dest f
}

/** Custom tasks: FFmpeg Download and deployment. */

task downloadFFmpeg(type: Download) {
    def f = new File('ffmpeg.zip')
    outputs.upToDateWhen {
        return f.exists()
    }

    def os = ""
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        os = "win"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        os = "osx"
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        os = "linux"
    }

    src "https://github.com/vot/ffbinaries-prebuilt/releases/download/v4.2.1/ffmpeg-4.2.1-$os-64.zip"
    dest f
}

task downloadFFprobe(type: Download) {
    def f = new File('ffprobe.zip')
    outputs.upToDateWhen {
        return f.exists()
    }

    def os = ""
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        os = "win"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        os = "osx"
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        os = "linux"
    }

    src "https://github.com/vot/ffbinaries-prebuilt/releases/download/v4.2.1/ffprobe-4.2.1-$os-64.zip"
    dest f
}

task copyFFmpeg(type: Copy) {
    dependsOn downloadFFmpeg
    outputs.upToDateWhen {
        return !fileTree('ext/ffmpeg').filter { it.isFile() && it.name.contains("ffmpeg")}.isEmpty()
    }
    from zipTree(downloadFFmpeg.dest)
    into 'ext/ffmpeg'
    include '*ffmpeg*'
}

task copyFFprobe(type: Copy) {
    dependsOn downloadFFprobe
    outputs.upToDateWhen {
        return !fileTree('ext/ffmpeg').filter { it.isFile() && it.name.contains("ffprobe")}.isEmpty()
    }
    from zipTree(downloadFFprobe.dest)
    into 'ext/ffmpeg'
    include '*ffprobe*'
}

task setupFFMpeg(type: Copy) {
    dependsOn downloadFFmpeg
    dependsOn downloadFFprobe
    dependsOn copyFFmpeg
    dependsOn copyFFprobe
}

/** Distribution tasks depend on Jar and setupFFMpeg. */
distZip.dependsOn jar
distZip.dependsOn setupFFMpeg
distTar.dependsOn jar
distTar.dependsOn setupFFMpeg

//tasks.openApiGenerate.dependsOn downloadOAS