buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    /// Download task for FFMPEG binaries download
    id 'de.undercouch.download' version '4.0.4'

    /// OpenAPI Generator for Frontend internal api generation
    id 'org.openapi.generator' version '5.2.0'
}

/// Variables used for Open API generation.
def fullOAS = 'http://localhost:8080/swagger-docs'
def clientOAS = 'http://localhost:8080/client-oas'
def oasFile = "${project.projectDir}/doc/oas.json"
def clientOasFile = "${project.projectDir}/doc/oas-client.json"

/// Generates the openapi frontend bindings
openApiGenerate {
    /// Source command:
    /// openapi-generator generate -g typescript-angular -i http://localhost:8080/swagger-docs -o openapi --skip-validate-spec --additional-properties npmName=@dres-openapi/api,snapshot=true,ngVersion=9.1.0
    generateApiTests = false // No tests please
    generateModelTests = false // No tests please
    validateSpec = false // No validation please (as in command above)

    generatorName = 'typescript-angular'
    inputSpec = oasFile
    outputDir = file("${project.projectDir}/frontend/openapi").toString()
    configOptions = [
        npmName: '@dres-openapi/api',
        ngVersion: '13.2.3',
        snapshot: 'true', /// I suggest to remove this, as soon as we automate this,
        enumPropertyNaming: 'original'
    ]
}

task generateOAS(type: Download) {
    /* Requires DRES running on default port */
    def f = new File(oasFile)
    src fullOAS
    dest f
}

task generateClientOAS(type: Download){
    def f = new File(clientOasFile)
    src clientOAS
    dest f
}
